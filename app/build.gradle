plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
//    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin"
    id 'io.gitlab.arturbosch.detekt'
    id 'androidx.navigation.safeargs'
    id 'com.google.devtools.ksp' version '1.6.20-RC-1.0.4'
    id "org.jetbrains.kotlin.plugin.serialization"
    id "scabbard.gradle" version "0.5.0"
}
ext.versionMajor = 1
ext.versionMinor = 2
ext.versionPatch = 0

@SuppressWarnings('GrMethodMayBeStatic')
def generateVersionCode() {
    return ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

def generateVersionName() {
    String versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
    def versionPropsFile = file('version.properties')
    Properties versionProps = new Properties()

    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
    } else {
        versionProps['BUILD_NUMBER'] = '0'
    }

    def buildNumber = versionProps['BUILD_NUMBER'].toInteger() + 1
    versionProps['BUILD_NUMBER'] = buildNumber.toString()
    versionProps.store(versionPropsFile.newWriter(), null)

    return "${versionName}.BN-${buildNumber}"
}

android {
    compileSdk sdk

    defaultConfig {
        applicationId "com.shop.tcd"
        minSdk 22
        targetSdk 31
        versionCode generateVersionCode()
        versionName generateVersionName()
        vectorDrawables {
            useSupportLibrary true
        }
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    buildTypes {
        debug{
            minifyEnabled false
            shrinkResources false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs = ['-Xjvm-default=all'] //enable or compatibility
    }
    buildToolsVersion '32.1.0 rc1'
    ndkVersion '21.4.7075529'
}
detekt {
    toolVersion = "1.19.0"
    buildUponDefaultConfig = false
    allRules = false
    config = files("$rootDir/detekt/detekt.yml")
    baseline = file("$rootDir/detekt/baseline.xml")
    input = files("src/main/java/com")
    debug = true
    parallel = true
}
dependencies {
    implementation "com.bugsnag:bugsnag-plugin-android-okhttp:$bugsnag"
    debugImplementation "com.bugsnag:bugsnag-android:$bugsnag"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary"
    implementation "androidx.appcompat:appcompat:$appcompat"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout"
    implementation "androidx.core:core-ktx:$core_ktx"
    implementation "io.coil-kt:coil:$coil"
    implementation "io.coil-kt:coil-svg:$coil"
    implementation "io.coil-kt:coil-gif:$coil"
    implementation "androidx.datastore:datastore-preferences-rxjava3:$datastore"
    implementation "androidx.datastore:datastore-preferences:$datastore"
    implementation "androidx.datastore:datastore-rxjava3:$datastore"
    implementation "androidx.datastore:datastore:$datastore"
    implementation "androidx.fragment:fragment-ktx:$fragment_ktx"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle"
    implementation "androidx.navigation:navigation-compose:$navigation"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$navigation"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation"
    implementation "androidx.navigation:navigation-runtime:$navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation"
    implementation "androidx.room:room-guava:$room"
    implementation "androidx.room:room-ktx:$room"
    implementation "androidx.room:room-paging:$room"
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-rxjava3:$room"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swiperefreshlayout"
    implementation "androidx.work:work-multiprocess:$work"
    implementation "androidx.work:work-runtime-ktx:$work"
    implementation "androidx.work:work-rxjava3:$work"
    implementation "com.github.bumptech.glide:glide:$glide"
    implementation "com.github.karczews:rx2-broadcast-receiver:$broadcast_receiver"
    implementation "com.google.android.material:material:$material"
    implementation "com.google.code.gson:gson:$gson"
    implementation "com.google.dagger:dagger:$dagger"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "com.google.dagger:dagger-compiler:$dagger"
    implementation "com.google.dagger:dagger-android:$dagger"
    implementation "com.google.dagger:dagger-android-support:$dagger"
    kapt "com.google.dagger:dagger-android-processor:$dagger"
    implementation "com.jakewharton.timber:timber:$timber"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp"
    implementation "com.squareup.okhttp3:okhttp:$okhttp"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit"
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "io.github.shashank02051997:FancyToast:$fancy"
    implementation "io.reactivex.rxjava3:rxandroid:$rxandroid"
    implementation "io.reactivex.rxjava3:rxjava:$rxjava"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$serialization_json"
    kapt "com.github.bumptech.glide:compiler:$glide"
    ksp "androidx.room:room-compiler:$room"
}
scabbard {
    enabled false
    failOnError true
    fullBindingGraphValidation true
    qualifiedNames false
    outputFormat "svg"
}
kotlin {
    sourceSets {
        main.kotlin.srcDirs += 'build/generated/ksp/main/kotlin'
        test.kotlin.srcDirs += 'build/generated/ksp/test/kotlin'
    }
}
tasks.named("detekt").configure {
    reports {
        html.required.set(true)
        html.outputLocation.set(file("build/reports/detekt.html"))
    }
}